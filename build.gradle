apply from: "config.gradle"

group rootProject.ext.app.group
version rootProject.ext.app.version

buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

apply plugin: 'idea'

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        provided
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    sourceSets {
        main {
            scala {
                srcDirs = ['src/main/java', 'src/main/scala']
            }
            java {
                srcDirs = []
            }
        }

        test {
            scala {
                srcDirs = ['src/test/java', 'src/test/scala']
            }
            java {
                srcDirs = []
            }
        }
    }

    dependencies {
        compile rootProject.ext.dependencies['scala-library'],
                rootProject.ext.dependencies['akka-cluster-sharding'],
                rootProject.ext.dependencies['akka-slf4j']
    }

    tasks.withType(ScalaCompile) {
        configure(scalaCompileOptions.forkOptions) {
            memoryMaximumSize = '1g'
            jvmArgs = ['-XX:MaxMetaspaceSize=512m']
        }
        scalaCompileOptions.with {
            encoding = "UTF-8"
            force = true
        }
    }
}
